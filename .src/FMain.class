' Gambas class file

' Copyright (C) 2004-2024 Murilo Gomes Julio
' SPDX-License-Identifier: GPL-2.0-only

' Mestre da Info
' Site: https://www.mestredainfo.com.br

Private Function GerarChave() As String
  
  Dim lmin As String = "abcdefghijklmnopqrstuvwxyz"
  Dim lmai As String = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
  Dim num As String = "1234567890"
  Dim esp As String = "!@#$%*"
  
  Dim retorno As String = ""
  Dim caracteres As String = ""
  
  If chkLetrasMinusculas.Value = True Then 
    caracteres &= lmin
  Endif 
  
  If chkLetrasMaiusculas.Value = True Then 
    caracteres &= lmai
  Endif 
  
  If chkNumeros.Value = True Then 
    caracteres &= num
  Endif 
  
  If chkCaracteresEspeciais.Value = True Then 
    caracteres &= esp
  Endif
  
  If chkLetrasMinusculas.Value = False And chkLetrasMaiusculas.Value = False And chkNumeros.Value = False And chkCaracteresEspeciais.Value = False Then 
    caracteres &= lmin
    caracteres &= lmai
    caracteres &= num
  Endif
  
  Dim len As Integer = Len(caracteres)
  
  Dim segmento As Integer = txtSegmentos.Value
  Dim tamanho As Integer = txtTamanho.Value
  
  Dim a As Integer = 0
  Dim n As Integer = 0
  
  Dim rand As Integer
  
  Do While a <= tamanho
    n = 0
    Do While n <= segmento
      rand = Rand(1, len)
      retorno &= caracteres[rand - 1]
      Inc n
      If n = segmento Then Break
    Loop
    retorno &= "-"
    Inc a
    If a = tamanho Then Break
  Loop
  
  retorno = Left(retorno, Len(retorno) - 1)
  Return retorno
  
End

Public Sub Form_Open()
  
  txtSegmentos.Value = 5
  txtTamanho.Value = 5  
  
End

Public Sub btnCopiar1_Click()
  
  Clipboard.Copy(txtKey.Text)
  
End

Public Sub btnCopiar2_Click()
  
  Clipboard.Copy(txtHash.Text)
  
End

Private Function getHash(tipo As String, valor As String) As String 
  
  Dim sHash As String 
  
  Shell "echo '" & valor & "' | " & Lower(tipo) & "sum" To sHash
  
  sHash = RTrim(sHash)
  sHash = Left(sHash, Len(sHash) - 1)
  sHash = RTrim(sHash)
  Return sHash
  
End

Public Sub btnGerar_Click()
  
  Dim sKey As String
  
  If txtSegmentos.Value <= 0 Then 
    txtSegmentos.Value = 1
  Endif
  
  If txtTamanho.Value <= 0 Then 
    txtTamanho.Value = 1
  Endif
  
  btnGerar.Enabled = False
  
  txtKey.Text = "Generating..."
  txtHash.Text = "Generating..."
  
  Wait 0.5
  
  sKey = GerarChave()
  
  txtKey.Text = sKey
  txtHash.Text = getHash(cboTipoHash.Text, sKey)
  
  btnGerar.Enabled = True
  mnuSave.Enabled = True
  
End

Public Sub mnuSave_Click()
  
  Dim txt As String = Subst(("Key: &1"), txtKey.Text) & gb.NewLine
  
  txt &= Subst("Hash: &1", txtHash.Text)
  
  Dialog.Title = ("Save File")
  Dialog.Filter = ["*.txt", ("Text File")]
  
  If Dialog.SaveFile() = False Then 
    File.Save(Dialog.Path, txt)
  Endif
  
End

Public Sub mnuClose_Click()
  
  Quit
  
End

Public Sub mnuApoie_Click()
  
  Dim sTerm As String = ""
  
  Shell "xdg-open \"https://www.mestredainfo.com.br/p/apoie.html\"" To sTerm
  
End

Public Sub mnuCheckUpdate_Click()
  
  Dim sTerm As String = ""
  
  Shell "xdg-open \"https://www.mestredainfo.com.br/2024/12/mikeygenerator.html\"" To sTerm
  
End
